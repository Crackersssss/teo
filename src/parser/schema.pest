schema = {
    SOI
    ~ (config_declaration | enum_declaration | model_declaration | comment_block | let_declaration | EMPTY_LINES | CATCH_ALL)*
    ~ EOI
}

// #############
// constants
// #############
WHITESPACE = _{ SPACE_SEPARATOR | "\t" }
EMPTY_LINES = @{ (WHITESPACE* ~ NEWLINE)+ }
BLOCK_OPEN = { "{" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
BLOCK_CLOSE = { "}" }
ENUM_KEYWORD = { "enum" }
MODEL_KEYWORD = { "model" }
CONFIG_KEYWORD = { "config" }
CONNECTOR_KEYWORD = { "connector" }
CLIENT_KEYWORD = { "client" }
GENERATOR_KEYWORD = { "generator" }
IMPORT_KEYWORD = { "import" }
FROM_KEYWORD = { "from" }
COLON = { ":" }
TRAILING_COMMA = @{ "," }
RANGE_OPEN = { ".." }
RANGE_CLOSE = { "..." }
CATCH_ALL = { (!NEWLINE ~ ANY)+ ~ NEWLINE? }
BLOCK_LEVEL_CATCH_ALL = { !BLOCK_CLOSE ~ CATCH_ALL }

// #############
// identifier & path
// #############
identifier = @{ ASCII_ALPHANUMERIC ~ ( "_" | "-" | ASCII_ALPHANUMERIC)* }
path = { identifier ~ ("." ~ path?)* }

// #############
// type
// #############
field_type = { identifier ~ optionality? ~ arity? ~ optionality? }
arity = {"[]" | "{}" }
optionality = { "?" }

// #############
// import
// #############

import_statement = {
    IMPORT_KEYWORD
    ~ (import_identifier_list ~ FROM_KEYWORD)?
    ~ string_literal
}

import_identifier_list = { BLOCK_OPEN ~ (identifier ~ ("," ~ identifier)*)? ~ TRAILING_COMMA? ~ BLOCK_CLOSE }

// #############
// numeric literals
// #############
numeric_literal = @{ ("-")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

// #############
// string literals
// #############
ASCII_CONTROL_CHARACTER = _{ '\u{0000}'..'\u{001F}' }
string_escape = _{ "\\" ~ ANY }
string_content = @{ (string_escape | !("\"" | ASCII_CONTROL_CHARACTER) ~ ANY)* }
string_literal = ${ "\"" ~ string_content ~ "\"" }

// #############
// regexp literals
// #############
regexp_content = @{ (string_escape | !("/") ~ ANY)+ }
regexp_literal = ${ "/" ~ string_content ~ "/" }

// #############
// bool literals
// #############
bool_literal = @{ "true" | "false" }

// #############
// null literals
// #############
null_literal = { "null" }

// #############
// enum choice literals
// #############
enum_choice_literal = @{ "." ~ identifier }

// #############
// range literals
// #############
range_literal = { range_end ~ (RANGE_OPEN | RANGE_CLOSE) ~ range_end }
range_end = { numeric_literal | call | path | subscript | chain_without_range_literal }

// #############
// tuple literals
// #############
tuple_literal = { "(" ~ ("," | (expression ~ "," ~ ( expression ~ ("," ~ expression)* ~ ","?)*)) ~ ")" }

// #############
// array literals
// #############
array_literal = { "[" ~ (expression ~ ( "," ~ expression )*)? ~ "]" }

// #############
// dict literals
// #############
named_expression = { expression ~ ":" ~ expression }
dictionary_literal = { "{" ~ (named_expression ~ ( "," ~ named_expression )*)? ~ "}" }

// #############
// subscript
// #############
subscript_item = { "[" ~ expression ~ "]" }
subscript = { (path | array_literal | dictionary_literal) ~ subscript_item+ }
path_subscript = { path ~ subscript_item+ }

// #############
// comments
// #############
doc_content = @{ (!NEWLINE ~ ANY)* }
triple_comment = { WHITESPACE* ~ "///" ~ doc_content }
double_comment = { WHITESPACE* ~ (!"///") ~ "//" ~ doc_content }
comment_block = ${ ((triple_comment | double_comment) ~ NEWLINE?)+ }
trailing_comment = ${ triple_comment | double_comment }

// #############
// operators
// #############
OPERATORS = { "??" | "+" | "-" | "*" | "/" | "%" | "!" | "&" | "|" | "&&" | "||" | ">" | "<" | ">>" | "<<" }
mul = { expression ~ (("*" | "/" | "%") ~ expression)+ }
add = { (mul | expression) ~ (("+" | "-") ~ (mul | expression))+ }
operand = { null_literal | bool_literal | numeric_literal | string_literal | regexp_literal | enum_choice_literal | tuple_literal | array_literal | dictionary_literal | range_literal | call | pipeline | path | subscript | chain }
nullish_coalescing = { operand ~ ("??" ~ operand)+ }

// #############
// expression
// #############

group = { "(" ~ expression ~ ")" }

literal_without_range = { null_literal | bool_literal | numeric_literal | string_literal | regexp_literal | enum_choice_literal | tuple_literal | array_literal | dictionary_literal }
call = { path ~ arguments_list+ }

chain = { null_literal | bool_literal | numeric_literal | string_literal | regexp_literal | enum_choice_literal | tuple_literal | array_literal | dictionary_literal | range_literal | path | call | subscript ~ (subscript_item | arguments_list | "." ~ path | call | subscript)* }
chain_without_range_literal = { literal_without_range | path | call | subscript ~ (subscript_item | arguments_list | "." ~ path | call | subscript)* }

expression = {
    nullish_coalescing |
    null_literal |
    bool_literal |
    numeric_literal |
    string_literal |
    regexp_literal |
    enum_choice_literal |
    tuple_literal |
    array_literal |
    dictionary_literal |
    range_literal |
    path |
    call |
    pipeline |
    subscript |
    chain
}

// #############
// variable & constant declaration
// #############

let_declaration = { "let" ~ identifier ~ "=" ~ expression }

// #############
// arguments
// #############
arguments_list = { "(" ~ (argument ~ ("," ~ argument)*)? ~ TRAILING_COMMA? ~ ")" }
argument = _{ named_argument | empty_argument | expression }
empty_argument = { identifier ~ ":" }
named_argument = { identifier ~ ":" ~ expression }

// #############
// function call, decorator and pipeline
// #############
item_decorator = { "@" ~ path ~ arguments_list? }
block_decorator = { "@@" ~ path ~ arguments_list? }
pipeline = { "$" ~ call | path ~ ("." ~ call | path)* }

// #############
// config blocks
// #############

config_keywords = { CONFIG_KEYWORD | CONNECTOR_KEYWORD | CLIENT_KEYWORD | GENERATOR_KEYWORD }

config_declaration = {
    config_keywords
    ~ identifier?
    ~ BLOCK_OPEN
    ~ (config_item | comment_block | EMPTY_LINES)*
    ~ BLOCK_CLOSE
}

config_item = {
    identifier ~ expression
}

// #############
// enum
// #############
enum_declaration = {
    (item_decorator ~ (NEWLINE | WHITESPACE)*)*
    ~ ENUM_KEYWORD
    ~ identifier
    ~ BLOCK_OPEN
    ~ (enum_value_declaration | (block_decorator ~ NEWLINE) | comment_block | EMPTY_LINES | BLOCK_LEVEL_CATCH_ALL)*
    ~ BLOCK_CLOSE
}

enum_value_declaration = { item_decorator* ~ identifier ~ trailing_comment? ~ NEWLINE }

// #############
// model
// #############
model_declaration = {
    (item_decorator ~ (NEWLINE | WHITESPACE)*)*
    ~ MODEL_KEYWORD
    ~ identifier
    ~ BLOCK_OPEN
    ~ (field_declaration | (block_decorator ~ NEWLINE) | comment_block | EMPTY_LINES | BLOCK_LEVEL_CATCH_ALL)*
    ~ BLOCK_CLOSE
}

// #############
// field
// #############
field_declaration = {
    (item_decorator ~ (NEWLINE | WHITESPACE)*)*
    ~ identifier
    ~ COLON
    ~ field_type
    ~ trailing_comment?
    ~ NEWLINE
}
